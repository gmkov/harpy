import os

configfile: "config.yaml"
# user specified configs
seq_dir = config["seq_directory"]
nbins = config["EMA_bins"]
#bin_digits = len(str(nbins))
genomefile = config["genome_file"]

# this identifies whether .fastq.gz or .fq.gz is used as the file extension
fastqlist = [i for i in os.listdir(seq_dir) if i.endswith('.fastq.gz')]
fqext = "fq.gz" if not fastqlist else "fastq.gz"

Rlist = [i for i in os.listdir(seq_dir) if i.endswith('.R1.' + fqext)]
Rsep = "_R" if not Rlist else ".R"

# the samples config is generated by an external script that appends it to the config.yaml
samplenames = set([i.split('.')[0] for i in os.listdir(seq_dir) if i.endswith(fqext)])
#samplenames = [config["samples"][i]["name"] for i in config["samples"]]

#print("Samples detected: " + f"{len(samplenames)}")

rule all:
	input: 
		alignments = expand("ReadMapping/align/{sample}.bam", sample = samplenames),
		stats = expand("ReadMapping/align/stats/{sample}.stats", sample = samplenames),
		flagstat = expand("ReadMapping/align/flagstat/{sample}.flagstat", sample = samplenames)
	message: "Read mapping completed! Generating alignment reports ReadMapping/alignment.stats.html and ReadMapping/alignment.flagstat.html."
#	shell:
#		"""
#		multiqc ReadMapping/align/stats --force --quiet --filename ReadMapping/alignment.stats.html
#		multiqc ReadMapping/align/flagstat --force --quiet --filename ReadMapping/alignment.flagstat.html
#		"""

rule index_genome:
	input: genomefile
	output: multiext(genomefile, ".ann", ".bwt", ".fai", ".pac", ".sa", ".amb")
	message: "Indexing {input}"
	shell: 
		"""
		bwa index {input}
		samtools faidx {input}
		"""

rule ema_count:
	input:
		forward_reads = seq_dir + "/{sample}" + Rsep + "1." + fqext,
		reverse_reads = seq_dir + "/{sample}" + Rsep + "2." + fqext
	output: 
		counts = "ReadMapping/count/{sample}.ema-ncnt"
	wildcard_constraints:
		sample = "[a-zA-Z0-9_-]*"
	message: "Counting barcode frequency: {wildcards.sample}"
	log: "ReadMapping/count/logs/{sample}.count.log"
	params:
		prefix = lambda wc: "ReadMapping/count/" + wc.get("sample")
	threads: 1
	shell:
		"""
		emaInterleave {input.forward_reads} {input.reverse_reads} | ema-h count -p -o {params} 2> {log}
		"""

rule ema_preprocess:
	input: 
		forward_reads = seq_dir + "/{sample}" + Rsep + "1." + fqext,
		reverse_reads = seq_dir + "/{sample}" + Rsep + "2." + fqext,
		emacounts = "ReadMapping/count/{sample}.ema-ncnt"
	output: 
		bins = expand("ReadMapping/preproc/{{sample}}/ema-bin-{bin}", bin = ["%03d" % i for i in range(nbins)]),
		unbarcoded = temp("ReadMapping/preproc/{sample}/ema-nobc")
	wildcard_constraints:
		sample = "[a-zA-Z0-9_-]*"
	log: "ReadMapping/preproc/logs/{sample}.preproc.log"
	message: "Preprocessing for EMA mapping: {wildcards.sample}"
	threads: 2
	params:
		outdir = lambda wc: "ReadMapping/preproc/" + wc.get("sample"),
		bins = nbins
	shell:
		"""
		emaInterleave {input.forward_reads} {input.reverse_reads} | ema-h preproc -p -n {params.bins} -t {threads} -o {params.outdir} {input.emacounts} 2>&1 | cat - > {log}
		"""

rule ema_align:
	input:
		readbin = "ReadMapping/preproc/{sample}/ema-bin-{bin}",
		genome = genomefile,
		genome_idx = multiext(genomefile, ".ann", ".bwt", ".fai", ".pac", ".sa", ".amb")
	output: pipe("ReadMapping/align/{sample}/{sample}.{bin}.sam")
	wildcard_constraints:
		sample = "[a-zA-Z0-9_-]*"
	message: "Mapping onto {input.genome}: {wildcards.sample}-{wildcards.bin}"
	threads: 1
	shell:
		"""
		ema-h align -t {threads} -d -p haptag -r {input.genome} -o {output} -R "@RG\\tID:{wildcards.sample}\\tSM:{wildcards.sample}" -s {input.readbin} 2> /dev/null
		"""

rule align_nobarcode:
	input:
		reads = "ReadMapping/preproc/{sample}/ema-nobc",
		genome = genomefile,
		genome_idx = multiext(genomefile, ".ann", ".bwt", ".fai", ".pac", ".sa", ".amb")
	output: 
		samfile = pipe("ReadMapping/align/{sample}/{sample}.nobarcode.sam")
	wildcard_constraints:
		sample = "[a-zA-Z0-9_-]*"
	message: "Mapping unbarcoded reads onto {input.genome}: {wildcards.sample}"
	threads: 2
	shell:
		"""
		bwa mem -p -t {threads} -M -R "@RG\\tID:{wildcards.sample}\\tSM:{wildcards.sample}" {input.genome} {input.reads} > {output} 2> /dev/null
		"""

rule ema_sort:
	input: "ReadMapping/align/{sample}/{sample}.{emabin}.sam"
	output: "ReadMapping/align/{sample}/{sample}.{emabin}.bam"
	wildcard_constraints:
		sample = "[a-zA-Z0-9_-]*",
		emabin = "[0-9]*"
	message: "Sorting alignments: {wildcards.sample}-{wildcards.emabin}"
	threads: 2
	shell: 
		"""
		samtools sort -@ {threads} -O bam -l 0 -m 4G -o {output} {input}
		"""

rule sort_nobarcode:
	input: "ReadMapping/align/{sample}/{sample}.nobarcode.sam"
	output: temp("ReadMapping/align/{sample}/{sample}.nobarcode.bam.tmp")
	wildcard_constraints:
		sample = "[a-zA-Z0-9_-]*"
	message: "Sorting unbarcoded alignments: {wildcards.sample}"
	threads: 2
	shell:
		"""
		samtools sort -@ {threads} -O bam -l 0 -m 4G -o {output} {input}
		"""    

rule markduplicates:
	input: "ReadMapping/align/{sample}/{sample}.nobarcode.bam.tmp"
	output: "ReadMapping/align/{sample}/{sample}.nobarcode.bam"
	wildcard_constraints:
		sample = "[a-zA-Z0-9_-]*"
	message: "Marking duplicates in unbarcoded alignments: {wildcards.sample} "
	threads: 2
	shell:
		"""
		sambamba markdup -t {threads} -l 0 {input} {output}
		"""   

rule merge_alignments:
	input:
		aln_barcoded = expand("ReadMapping/align/{{sample}}/{{sample}}.{bin}.bam", bin = ["%03d" % i for i in range(nbins)]),
		aln_nobarcode = "ReadMapping/align/{sample}/{sample}.nobarcode.bam"
	output: 
		bam = "ReadMapping/align/{sample}.bam",
		stats = "ReadMapping/align/stats/{sample}.stats",
		flagstat = "ReadMapping/align/flagstat/{sample}.flagstat"
	wildcard_constraints:
		sample = "[a-zA-Z0-9_-]*"
	message: "Merging alignments: {wildcards.sample}"
	threads: 10
	shell:
		"""
		sambamba merge -t {threads} {output.bam} {input}
		samtools stats {output.bam} > {output.stats}
		samtools flagstat {output.bam} > {output.flagstat}
		"""